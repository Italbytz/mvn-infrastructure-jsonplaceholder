plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "com.squareup.retrofit2:retrofit:2.6.0"
    implementation "com.squareup.retrofit2:converter-gson:2.6.0"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "io.github.robinnunkesser"
archivesBaseName = "explicitarchitecture-posts-infrastructure-retrofit"
version = "0.1.0"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Explicit Architecture Posts Infrastructure'
                packaging 'jar'
                description 'An example infrastructure implementation.'
                url 'https://github.com/RobinNunkesser/explicitarchitecture-posts-infrastructure-mvn'

                scm {
                    connection 'scm:git:git://github.com/RobinNunkesser/explicitarchitecture-posts-infrastructure-mvn.git'
                    developerConnection 'scm:git:ssh://github.com/RobinNunkesser/explicitarchitecture-posts-infrastructure-mvn.git'
                    url 'https://github.com/RobinNunkesser/explicitarchitecture-posts-infrastructure-mvn.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'Robin Nunkesser'
                        email 'robin.nunkesser@hshl.de'
                    }
                }
            }
        }
    }
}

